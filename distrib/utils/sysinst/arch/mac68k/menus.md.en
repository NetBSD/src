/*	$NetBSD: menus.md.en,v 1.5 1999/06/20 06:08:20 cgd Exp $	*/

/*
 * Copyright 1997 Piermont Information Systems Inc.
 * All rights reserved.
 *
 * Written by Philip A. Nelson for Piermont Information Systems Inc.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgement:
 *      This product includes software developed for the NetBSD Project by
 *      Piermont Information Systems Inc.
 * 4. The name of Piermont Information Systems Inc. may not be used to endorse
 *    or promote products derived from this software without specific prior
 *    written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY PIERMONT INFORMATION SYSTEMS INC. ``AS IS''
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL PIERMONT INFORMATION SYSTEMS INC. BE 
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR 
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF 
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF 
 * THE POSSIBILITY OF SUCH DAMAGE.
 *
 */

/* Menu definitions for sysinst. mac68k version, machine dependent. */

menu fullpart, title  "Select your choice";
	option "Use only part of the disk", exit, action  {usefull = 0;};
	option "Use the entire disk", 	    exit, action  {usefull = 1;};

menu nodiskmap, title "Choose an option", y=16;
	display action { msg_display (MSG_nodiskmap, diskdev); };
	option "Abort install",
		action (endwin)  { exit(1); };
	option "Initialize Disk partition Map", action {
		int i;

		msg_clear();
		msg_display (MSG_okwritediskmap);
		process_menu (MENU_okabort);
		if (!yesno) {
		    endwin();
		    return 0;
		}
	 	map.size = NEW_MAP_SIZE;
		map.in_use_cnt = new_map[0].pmMapBlkCnt;
		map.blk = (struct part_map_entry *)calloc(map.size,
			sizeof(struct part_map_entry));
		for (i=0;i<map.size;i++)
		    memcpy (&map.blk[i], &new_map[i],
			 sizeof(struct part_map_entry));
 	};

menu editparttable, title  "Choose your partition", exit, y=15;
	display action  { msg_display (MSG_editparttable);
			  sortmerge();
			  if (map.selected >= map.usable_cnt)
				map.selected = 0;
                          disp_selected_part (map.selected);
			};
	option "Select next partition",  action {
		map.selected += 1;
		if (map.selected >= map.usable_cnt)
			map.selected = 0;
		};
	option "Change selected partition", sub menu chooseid;
	option "Set mount point for partition", sub menu mount_point;
	option "Split selected partition", action {
		int i, j, k, size, free_size;
		char buf[40];

		j = map.mblk[map.selected];
		msg_display(MSG_split_part, map.blk[j].pmPartBlkCnt);
		msg_prompt_add (MSG_scratch_size, NULL, buf, sizeof(buf));
		size = atoi(buf);
		if (size > 0 && size < map.blk[j].pmPartBlkCnt) {
		    k = map.in_use_cnt+1;
		    if (k <= map.size) {
			memcpy (&map.blk[k], &map.blk[j],
			    sizeof(struct part_map_entry));
			free_size = map.blk[j].pmPartBlkCnt - size;
			strcpy (map.blk[j].pmPartType, "Apple_Scratch");
			map.blk[j].pmPartBlkCnt = size;
			map.blk[j].pmDataCnt = size;
			strcpy (map.blk[k].pmPartType, "Apple_Free");
			map.blk[k].pmPyPartStart += size;
			if ((map.blk[k].pmPyPartStart + free_size) > dlsize)
			    map.blk[k].pmPartBlkCnt = 
				dlsize - map.blk[k].pmPyPartStart;
			else
			    map.blk[k].pmPartBlkCnt = free_size;
			map.blk[k].pmDataCnt = map.blk[k].pmPartBlkCnt;
			map.in_use_cnt += 1;
		    } else {
			msg_display (MSG_diskfull);
			process_menu (MENU_okabort);
		 	if (!yesno) {
			    free (map.blk);
	 		    map.size = NEW_MAP_SIZE;
			    map.in_use_cnt = new_map[0].pmMapBlkCnt;
			    map.blk = (struct part_map_entry *)calloc(map.size,
				sizeof(struct part_map_entry));
			    for (i=0;i<map.size;i++)
		    		memcpy (&map.blk[i], &new_map[i],
			 	    sizeof(struct part_map_entry));
			    map.blk[0].pmSigPad = 0; /* Don't rewrite Block0 */
			}
		    }
		} };
        option "Fix selected partition", action {
                int i = map.mblk[map.selected];
                EBZB *bzb = (EBZB *)&map.blk[i].pmBootArgs[0];
                msg_display(MSG_partdebug, diskdev, bzb->flags.part,
                        map.blk[i].pmPyPartStart,
                        map.blk[i].pmPartBlkCnt);
                if ((map.blk[i].pmPyPartStart +
                    map.blk[i].pmPartBlkCnt) > dlsize) {
                        msg_printf_add("Fixing partition %s%c\n",
                                diskdev, bzb->flags.part);
                        map.blk[i].pmPartBlkCnt =
                            dlsize - map.blk[i].pmPyPartStart;
                        map.blk[i].pmDataCnt =
                            map.blk[i].pmPartBlkCnt;
                } else {
                    msg_printf_add("Partition %s%c is just fine!\n",
                        diskdev, bzb->flags.part);
                }
                process_menu(MENU_ok);
                };


menu okabort, title "What do you want to do?";
	option "OK", exit, action { yesno = 1; };
	option "Abort install", exit, action { yesno = 0; };

menu chooseid, title  "Partition Type?";
	option "NetBSD Root", exit, action {
		int i, j;
		char fstyp[16], use[16], name[64];
		EBZB *bzb;

		j = map.mblk[map.selected];
		reset_part_flags(j);
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->magic = BZB_MAGIC;
		strcpy (map.blk[j].pmPartType, "Apple_Unix_SVR2");
		bzb->type = BZB_TYPEFS;
		bzb->flags.root = 1;
		/*
		 * Automatically determine root mount points.  The first
		 *  root-type filesystem is mounted on "/", all others
		 *  will mount on "/altroot".  If there are multiple
		 *  occurances of "/altroot" they will be picked up on
		 *  the sanity_scan in the next step of the installation.
		 */
		for (i=0,map.root_cnt=0;i<map.usable_cnt;i++) {
		    j = map.mblk[i];
		    if (part_type(j, fstyp, use, name) == TYP_BSD) {
			bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
			if (bzb->type == BZB_TYPEFS && bzb->flags.root) {
			   if (map.root_cnt++ == 0) 
				strcpy (bzb->mount_point, "/");
			   else
				strcpy (bzb->mount_point, "/altroot");
			}
		    }
		} };
	option "NetBSD SWAP", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		reset_part_flags(j);
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->magic = BZB_MAGIC;
		strcpy (map.blk[j].pmPartType, "Apple_Unix_SVR2");
		bzb->type = BZB_TYPESWAP; };
	option "NetBSD Usr", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		reset_part_flags(j);
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->magic = BZB_MAGIC;
		strcpy (map.blk[j].pmPartType, "Apple_Unix_SVR2");
		bzb->type = BZB_TYPEFS;
		bzb->flags.usr = 1;
		if (!map.usr_cnt)
		    strcpy (bzb->mount_point, "/usr");
		};
	option "NetBSD Root&Usr", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		reset_part_flags(j);
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->magic = BZB_MAGIC;
		strcpy (map.blk[j].pmPartType, "Apple_Unix_SVR2");
		bzb->type = BZB_TYPEFS;
		bzb->flags.root = 1;
		bzb->flags.usr = 1;
		if (!map.root_cnt)
		    strcpy (bzb->mount_point, "/");
		else {
                    if (!map.usr_cnt)
		        strcpy (bzb->mount_point, "/usr");
		} };
	option "MacOS HFS", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		reset_part_flags(j);
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->mount_point[0] = '\0';
		strcpy (map.blk[j].pmPartType, "Apple_HFS"); };
	option "Scratch", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		reset_part_flags(j);
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->mount_point[0] = '\0';
		strcpy (map.blk[j].pmPartType, "Apple_Scratch"); };
	option "Free", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		reset_part_flags(j);
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->mount_point[0] = '\0';
		strcpy (map.blk[j].pmPartType, "Apple_Free"); };

menu mount_point, title  "Mount Point?";
	option "/usr", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		strcpy (bzb->mount_point, "/usr"); };
	option "/home", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		strcpy (bzb->mount_point, "/home"); };
	option "/var", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		strcpy (bzb->mount_point, "/var"); };
	option "/tmp", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		strcpy (bzb->mount_point, "/tmp"); };
	option "None", exit, action {
		int j;
		EBZB *bzb;

		j = map.mblk[map.selected];
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		bzb->mount_point[0] = '\0'; };
	option "other", exit, action {
		int j;
		char buf[60];
		EBZB *bzb;

		msg_display (MSG_custom_mount_point);
		msg_prompt_add (MSG_mountpoint, NULL, buf, sizeof(buf));
		j = map.mblk[map.selected];
		bzb = (EBZB *)&map.blk[j].pmBootArgs[0];
		if (buf[0] != '\0') {
		    bzb->mount_point[0] = '\0';
		    if (buf[0] != '/')
			strcpy (bzb->mount_point, "/");
		    strcat(bzb->mount_point, buf);
		} };

menu sanity, title "Choose an option";
	display action {msg_display (MSG_sanity_check);
		report_errors(); };
	option "Abort installation", exit, action {yesno = -1; };
	option "Ignore warnings and continue", exit, action {yesno = 1;};
	option "Re-edit Disk Partition Map", exit, action {yesno = 0; };

/*
 * This menu shouldn't be used in the mac68k port, but it needs to be
 *  defined because it's referenced from the toplevel code.
 */
menu editfsparts, y=13, exit;
	display action  {
			ask_sizemult();
			msg_display(MSG_fspart, multname);
			disp_cur_fspart(-1, 1);
		};
	option "Change a", action { editpart = A;}, sub menu edfspart;
	option "Change b", action { editpart = B;}, sub menu edfspart;
	option "Change e", action { editpart = E;}, sub menu edfspart;
	option "Change f", action { editpart = F;}, sub menu edfspart;
	option "Change g", action { editpart = G;}, sub menu edfspart;
	option "Change h", action { editpart = H;}, sub menu edfspart;
	option "Set new allocation size", action { reask_sizemult(); };

menu md_distcustom, y=3, exit, title "Selection toggles inclusion";
	display action { show_cur_distsets (); };
	option	"Kernel",		 action { toggle_getit (0); };
	option  "Kernel (SBC)",		 action { toggle_getit (1); };
	option	"Base",			 action { toggle_getit (2); };
	option  "System (/etc)",	 action { toggle_getit (3); };
	option  "Compiler Tools", 	 action { toggle_getit (4); };
	option  "Games", 		 action { toggle_getit (5); };
	option  "Online Manual Pages", 	 action { toggle_getit (6); };
	option  "Miscellaneous", 	 action { toggle_getit (7); };
	option  "Text Processing Tools", action { toggle_getit (8); };
	option  "X11 base and clients",	 action { toggle_getit (9); };
	option  "X11 fonts",		 action { toggle_getit (10); };
	option  "X11 servers",		 action { toggle_getit (11); };
	option  "X contrib clients",	 action { toggle_getit (12); };
	option  "X11 programming",	 action { toggle_getit (13); };

