#	$NetBSD: vchain,v 1.6.22.2 2017/12/03 11:38:44 jdolecek Exp $

#	@(#)vchain	8.1 (Berkeley) 6/10/93
#

define vchain
	set $num = 0

	set $vp=(struct vnode *)$arg0
	set $vi=(struct vnode_impl *)$arg0
	while ($vp)
		printf "vp: 0x%lx lrulist_next: 0x%lx usecount: %d flags: i:0x%x v:0x%x u:0x%x\n",\
		       $vp, $vi->vi_lrulist.tqe_next, $vp->v_uobj.uo_refs, \
		       $vp->v_iflag, $vp->v_vflag, $vp->v_uflag
		set $num++
		set $vp = $vp->v_mntvnodes.tqe_next
	end
	printf "Number of vnodes: %d\n", $num
end

document vchain
Given a vnode, follow its mount pointers
end

define vprint
	set $vp=(struct vnode *)$arg0
	set $ip=(struct inode *)$vp->v_data
end

define mp_vchain
	set $mp = (struct mount *)$arg0
	vchain $mp->mnt_vnodelist.tqh_first
end
document mp_vchain
print the vnode chain for a given mount point
end

define vall
	set $mp=mountlist.tqh_first
	while ($mp)
		printf "\tmount point at 0x%x\n", $mp
		mp_vchain $mp
		set $mp=$mp->mnt_list.tqe_next

		# "break"
		if ((const void *)$mp == (const void *)&mountlist)
			set $mp = 0
		end
	end
end
document vall
print vnode chains for all mount points
end

define mountdump
	set $me=mount_list.tqh_first
	while ($me)
		if ($me->me_type == ME_MOUNT)
			set $mp = $me->me_mount
			printf "%s on %s type %s, (mp 0x%x, privdata 0x%x)\n", \
			    $mp->mnt_stat->f_mntfromname, \
			    $mp->mnt_stat->f_mntonname, \
			    $mp->mnt_op->vfs_name, $mp, $mp->mnt_data
		end
		set $me=$me->me_list.tqe_next
		if ((const void *)$me == (const void *)&mount_list)
			set $me = 0
		end
	end
end
