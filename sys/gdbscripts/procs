#	$NetBSD: procs,v 1.4 2019/05/09 07:59:49 mrg Exp $

define procs
	set $i = 0

	while ($i < 2)
		if ($i == 0)
			set $p = allproc.lh_first
		end
		if ($p)
			printf "            proc   pid     flag stat nlwps            lwp  comm\n"
		end
		while ($p)
			printf "%16lx %5d %8x %4x %5d %16lx %s\n", \
				$p, $p->p_pid, \
				$p->p_flag, $p->p_stat, $p->p_nlwps, $p->p_lwps.lh_first, \
				(char *) $p->p_comm
			set $p = $p->p_list.le_next
		end
		set $i++
	end
end
document procs
list all processes.
displays struct proc *, pid, flags, status, nlwps, first lwp addr
and command name.
end
