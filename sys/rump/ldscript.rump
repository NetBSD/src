/*	$NetBSD: ldscript.rump,v 1.4 2020/03/21 04:48:38 pgoyette Exp $	*/

/*
 * From binutils 2.19 onwards (in NetBSD) binutils ld PROVIDEs
 * __start/__stop for orphaned sections.  This means that
 * __start_link_set_modules/__stop_link_set_modules will no
 * longer automatically be present in shared libraries.  This
 * ldscript forces those symbols to be present for all rump
 * shared lib components.
 */

SECTIONS
{
	link_set_modules :
	{
		__start_link_set_modules = .;
		*(link_set_modules);
		__stop_link_set_modules = .;
	}

	link_set_rump_components :
	{
		__start_link_set_rump_components = .;
		*(link_set_rump_components);
		__stop_link_set_rump_components = .;
	}

	link_set_sysctl_funcs :
	{
		__start_link_set_sysctl_funcs = .;
		*(link_set_sysctl_funcs);
		__stop_link_set_sysctl_funcs = .;
	}

	link_set_rump_components :
	{
		__start_link_set_evcnts = .;
		*(link_set_evcnts);
		__stop_link_set_evcnts = .;
	}
}
INSERT AFTER .data;
