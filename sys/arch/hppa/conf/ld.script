/*	$NetBSD: ld.script,v 1.1.10.3 2017/12/03 11:36:16 jdolecek Exp $	*/

/* 	$OpenBSD: ld.script,v 1.7 1999/12/17 07:02:38 mickey Exp $	*/

OUTPUT_FORMAT("elf32-hppa-netbsd")
OUTPUT_ARCH(hppa)
ENTRY(start)
SECTIONS
{
	. = + SIZEOF_HEADERS;
	.text :
	{
		kernel_text = . ;
		*(.text) *(.text.*)
		*(.reginfo)
		*(.init)
		*(.stub)
		/* .gnu.warning sections are handled specially by elf32.em.  */
		*(.gnu.warning)
		. = ALIGN(4096);
		etext = ABSOLUTE(.);
	} = 0x08000240 /* nop filled */

	__rodata_start = ABSOLUTE(.);
	.rodata	:
	{
		*(.rodata) *(.rodata.*) *(.rodata1)
		. = ALIGN(8);
	} = 0
	__unwind_start = .;
	.PARISC.unwind   : { *(.PARISC.unwind) }
	__unwind_end = .;

	/* Place (orphaned) link_sets */
	. = .;

	/* Align __rodata_end to page boundary */
	. = ALIGN(4096);
	__rodata_end = ABSOLUTE(.);

	. = ALIGN(0x200000);
	__data_start = ABSOLUTE(.);
	.data  :
	{
		$global$ = . ;
		. += 4;
		*(.data)
		CONSTRUCTORS
		. = ALIGN(4096);
		edata = ABSOLUTE(.);
	} = 0

	__bss_start = ABSOLUTE(.);
	/* .sbss	: { *(.sbss) *(.scommon) } */
	.bss :
	{
		*(.dynbss) *(.bss)
		*(COMMON) *($COMMON$)
		. = ALIGN(4096);
	}
	end = ABSOLUTE(.);
	.note.netbsd.ident :
	{
		KEEP(*(.note.netbsd.ident));
	}
}
