/*	$NetBSD: kern.ldscript,v 1.11.2.3 2016/05/29 08:44:17 skrll Exp $	*/

#include "assym.h"

__PAGE_SIZE = 0x1000 ;

ENTRY(_start)
SECTIONS
{
	.text :
	{
		*(.text)
		*(.text.*)
		*(.stub)
	}
	_etext = . ;
	PROVIDE (etext = .) ;

	. = ALIGN(__PAGE_SIZE);

	__rodata_start = . ;
	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
	}

	. = ALIGN(__PAGE_SIZE);

	__data_start = . ;
	.data :
	{
		*(.data)
	}

	. = ALIGN(COHERENCY_UNIT);
	.data.cacheline_aligned :
	{
		*(.data.cacheline_aligned)
	}
	. = ALIGN(COHERENCY_UNIT);
	.data.read_mostly :
	{
		*(.data.read_mostly)
	}
	. = ALIGN(COHERENCY_UNIT);

	_edata = . ;
	PROVIDE (edata = .) ;
	__bss_start = . ;
	.bss :
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(32 / 8);
	}

	. = ALIGN(__PAGE_SIZE);

	/* End of the kernel image */
	__kernel_end = . ;

	_end = . ;
	PROVIDE (end = .) ;
	.note.netbsd.ident :
	{
		KEEP(*(.note.netbsd.ident));
	}
}

SECTIONS
{
	.text :
	AT (ADDR(.text) & 0x0fffffff)
	{
		*(.text)
	} = 0
}
