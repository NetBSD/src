/*	$NetBSD: kern.ldscript.4MB,v 1.19 2018/01/07 12:42:47 maxv Exp $	*/

#include "assym.h"

/*
 * The large page size is 4MB in the non-PAE case.
 */

__PAGE_SIZE = 0x1000 ;
__LARGE_PAGE_SIZE = 0x400000 ;

ENTRY(_start)
SECTIONS
{
	.text : AT (ADDR(.text) & 0x0fffffff)
	{
		*(.text)
		*(.text.*)
		*(.stub)
		. = ALIGN(__LARGE_PAGE_SIZE);
	} =0xCC
	_etext = . ;
	PROVIDE (etext = .) ;

	/*
	 * Push the rodata segment up to the next large page boundary so that we
	 * can map the text segment with large pages.
	 */
	. = ALIGN(__LARGE_PAGE_SIZE);

	__rodata_start = . ;

	.rodata.hotpatch :
	{
		__rodata_hotpatch_start = . ;
		*(.rodata.hotpatch)
		__rodata_hotpatch_end = . ;
	}

	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
	}

	. = ALIGN(__PAGE_SIZE);

	__data_start = . ;
	.data :
	{
		*(.data)
	}

	. = ALIGN(COHERENCY_UNIT);
	.data.cacheline_aligned :
	{
		*(.data.cacheline_aligned)
	}
	. = ALIGN(COHERENCY_UNIT);
	.data.read_mostly :
	{
		*(.data.read_mostly)
	}
	. = ALIGN(COHERENCY_UNIT);

	_edata = . ;
	PROVIDE (edata = .) ;
	__bss_start = . ;
	.bss :
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(32 / 8);
	}

	. = ALIGN(__PAGE_SIZE);

	/* End of the kernel image */
	__kernel_end = . ;

	_end = . ;
	PROVIDE (end = .) ;
	.note.netbsd.ident :
	{
		KEEP(*(.note.netbsd.ident));
	}
}

