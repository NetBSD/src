/*	$NetBSD: stand.ldscript,v 1.1.12.1 2015/09/22 12:05:44 skrll Exp $	*/

ENTRY(_start)
SECTIONS
{
  /* Read-only sections, merged into text segment: */
  .text :
  {
    *(.text)
    *(.text.*)
    *(.stub)
  }
  _etext = . ;
  PROVIDE (etext = .) ;

  .rodata :
  {
    *(.rodata)
    *(.rodata.*)
  }

  __data_start = . ;
  .data :
  {
    *(.data)
  }
  .data.cacheline_aligned :
  {
    *(.data.cacheline_aligned)
  }
  .data.read_mostly :
  {
    *(.data.read_mostly)
  }
  _edata = . ;
  PROVIDE (edata = .) ;
  __bss_start = . ;
  .bss :
  {
    *(.bss)
    *(.bss.*)
    *(COMMON)
    . = ALIGN(32 / 8);
  }
  . = ALIGN(32 / 8);
  _end = . ;
  PROVIDE (end = .) ;
  /DISCARD/ : {
    *(.eh_frame_hdr)
    *(.eh_frame)
    *(.note.netbsd.ident)
  }
}
SECTIONS
{
  .text :
  AT (ADDR(.text) & 0x0fffffff)
  {
    *(.text)
  } =0
}
