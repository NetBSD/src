/*	$NetBSD: kern.ldscript.Xen,v 1.6.2.2 2016/05/29 08:44:17 skrll Exp $	*/

#include "assym.h"

__PAGE_SIZE = 0x1000 ;

SECTIONS
{
	/* Read-only sections, merged into text segment: */
	.text :
	{
		*(.text)
		*(.text.*)
		*(.stub)
	}
	_etext = . ;
	PROVIDE (etext = .) ;

	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
	}

	/*
	 * Adjust the address for the data segment.  We want to adjust up to
	 * the same address within the page on the next page up.
	 */
	. = ALIGN(0x1000) + (. & (0x1000 - 1));

	__rodata_start = . ;
	__data_start = . ;
	.data :
	{
		*(.data)
	}

	. = ALIGN(COHERENCY_UNIT);
	.data.cacheline_aligned :
	{
		*(.data.cacheline_aligned)
	}
	. = ALIGN(COHERENCY_UNIT);
	.data.read_mostly :
	{
		*(.data.read_mostly)
	}
	. = ALIGN(COHERENCY_UNIT);
	_edata = . ;

	PROVIDE (edata = .) ;
	__bss_start = . ;
	.bss :
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(32 / 8);
	}
	. = ALIGN(32 / 8);
	_end = . ;
	PROVIDE (end = .) ;
	.note.netbsd.ident :
	{
		KEEP(*(.note.netbsd.ident));
	}
}

SECTIONS
{
	.text :
	AT (ADDR(.text))
	{
		*(.text)
	} = 0
}
