/*	$NetBSD: kern.ldscript.Xen,v 1.3.60.1 2012/11/22 00:40:51 riz Exp $	*/

OUTPUT_FORMAT("elf32-i386", "elf32-i386",
	      "elf32-i386")
OUTPUT_ARCH(i386)
SECTIONS
{
   /* Read-only sections, merged into text segment: */
   .text :
   {
     *(.text)
     *(.text.*)
     *(.stub)
   } =0
   _etext = . ;
   PROVIDE (etext = .) ;

   .rodata :
   {
     *(.rodata)
     *(.rodata.*)
   }

   /* Adjust the address for the data segment.  We want to adjust up to
      the same address within the page on the next page up.  */
   . = ALIGN(0x1000) + (. & (0x1000 - 1));
   __data_start = . ;
   .data :
   {
     *(.data)
   }
   . = ALIGN(64);	/* COHERENCY_UNIT */
   .data.cacheline_aligned :
   AT (LOADADDR(.text) + (ADDR(.data.cacheline_aligned) - ADDR(.text)))
   {
     *(.data.cacheline_aligned)
   }
   . = ALIGN(64);	/* COHERENCY_UNIT */
   .data.read_mostly :
   AT (LOADADDR(.text) + (ADDR(.data.read_mostly) - ADDR(.text)))
   {
     *(.data.read_mostly)
   }
   . = ALIGN(64);	/* COHERENCY_UNIT */
   _edata = . ;
   PROVIDE (edata = .) ;
   __bss_start = . ;
   .bss :
   {
     *(.bss)
     *(.bss.*)
     *(COMMON)
     . = ALIGN(32 / 8);
   }
   . = ALIGN(32 / 8);
  _end = . ;
  PROVIDE (end = .) ;
  .note.netbsd.ident :
  {
    KEEP(*(.note.netbsd.ident));
  }
}
