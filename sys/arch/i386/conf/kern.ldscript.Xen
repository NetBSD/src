/*	$NetBSD: kern.ldscript.Xen,v 1.13.8.2 2018/03/06 10:17:12 martin Exp $	*/

#include "assym.h"

__PAGE_SIZE = 0x1000 ;

SECTIONS
{
	.text :
	{
		*(.text)
		*(.text.*)
		*(.stub)
	}
	_etext = . ;
	PROVIDE (etext = .) ;

	. = ALIGN(__PAGE_SIZE);

	__rodata_start = . ;

	.rodata.hotpatch :
	{
		__rodata_hotpatch_start = . ;
		*(.rodata.hotpatch)
		__rodata_hotpatch_end = . ;
	}

	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
	}

	. = ALIGN(__PAGE_SIZE);

	__data_start = . ;
	.data :
	{
		*(.data)
	}

	. = ALIGN(COHERENCY_UNIT);
	.data.cacheline_aligned :
	{
		*(.data.cacheline_aligned)
	}
	. = ALIGN(COHERENCY_UNIT);
	.data.read_mostly :
	{
		*(.data.read_mostly)
	}
	. = ALIGN(COHERENCY_UNIT);

	_edata = . ;
	PROVIDE (edata = .) ;
	__bss_start = . ;
	.bss :
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(32 / 8);
	}
	. = ALIGN(32 / 8);
	_end = . ;
	PROVIDE (end = .) ;

	. = ALIGN(__PAGE_SIZE);

	/* End of the kernel image */
	__kernel_end = . ;

	.note.netbsd.ident :
	{
		KEEP(*(.note.netbsd.ident));
	}
}

SECTIONS
{
	.text :
	AT (ADDR(.text))
	{
		*(.text)
	} = 0
}
