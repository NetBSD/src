/*	$NetBSD: boot.ldscript,v 1.5 2013/01/26 16:00:04 tsutsui Exp $	*/

OUTPUT_FORMAT("elf32-m68k")
OUTPUT_ARCH(m68k)
ENTRY(start)
SECTIONS
{
  /* Read-only sections, merged into text segment: */
  .text :
  {
    *(.text)
    *(.text.*)
    *(.rodata) *(.rodata.*)
    /* The data and bss sections are 8 byte aligned on ELF format,
       but a.out header doesn't have section addresses and only
       contains size values, so we have to pad size of text section
       to make data section get aligned even after elf2aout. */
    . = ALIGN(8);
  } =0
  PROVIDE (__etext = .);
  PROVIDE (_etext = .);
  PROVIDE (etext = .);
  .data :
  {
    __data_start = . ;
    *(.data)
    *(.data.*)
    *(.sdata)
    *(.sdata.*)
  }
  _edata = .;
  PROVIDE (edata = .);
  __bss_start = .;
  __bss_start__ = .;
  .bss       :
  {
   *(.dynbss)
   *(.bss)
   *(.bss.*)
   *(COMMON)
   /* Align here to ensure that the .bss section occupies space up to
      _end.  Align after .bss to ensure correct alignment even if the
      .bss section disappears because there are no input sections.  */
/*   . = ALIGN(32 / 8); */
  }
/*  . = ALIGN(32 / 8);*/
  _end = .;
  _bss_end__ = . ; __bss_end__ = . ; __end__ = . ;
  PROVIDE (end = .);
}
