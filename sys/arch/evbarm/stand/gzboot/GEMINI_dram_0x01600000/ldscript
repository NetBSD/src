/*	$NetBSD: ldscript,v 1.2 2009/09/26 07:29:55 skrll Exp $	*/

OUTPUT_FORMAT("elf32-littlearm", "elf32-bigarm",
	      "elf32-littlearm")
OUTPUT_ARCH(arm)
ENTRY(GZBOOT)
MEMORY
{
  /* We will locate the .text section in flash, and will run directly
     from there just long enough to relocate our .text and .data into
     a small chunk of SDRAM starting at (SDRAM + 1M).  */
  gzboot : o = 0x01600000, l = 6M
  sdram : o = 0x00200000, l = 2M	/* kernel loads at 0x00200000 */
}
SECTIONS
{
  GZBOOT = 0x01600000;

  /* Read-only sections, merged into text segment: */
  __text_store = GZBOOT;
  .text      :
  AT (GZBOOT)
  {
    *(.text)
    *(.text.*)
    *(.stub)
    *(.glue_7t) *(.glue_7)
    *(.rodata) *(.rodata.*)
  } > gzboot =0
  PROVIDE (__etext = .);
  PROVIDE (_etext = .);
  PROVIDE (etext = .);
  __data_store = GZBOOT + SIZEOF(.text);
  .data    :
  AT (LOADADDR(.text) + SIZEOF(.text))
  {
    __data_start = . ;
    *(.data)
    *(.data.*)
  } > gzboot
  .sdata     : 
  AT (LOADADDR(.data) + SIZEOF(.data))
  {
    *(.sdata) 
    *(.sdata.*)
    . = ALIGN(32 / 8);
  } > gzboot
  _edata = .;
  PROVIDE (edata = .);
  __bss_start = .;
  __bss_start__ = .;
  .sbss      :
  AT (ADDR(.sbss))
  {
    PROVIDE (__sbss_start = .);
    PROVIDE (___sbss_start = .);
    *(.dynsbss)
    *(.sbss)
    *(.sbss.*)
    *(.scommon)
    PROVIDE (__sbss_end = .);
    PROVIDE (___sbss_end = .);
  } > gzboot
  .bss       :
  AT (ADDR(.bss))
  {
   *(.dynbss)
   *(.bss)
   *(.bss.*)
   *(COMMON)
   /* Align here to ensure that the .bss section occupies space up to
      _end.  Align after .bss to ensure correct alignment even if the
      .bss section disappears because there are no input sections.  */
   . = ALIGN(32 / 8);
  } > gzboot
  . = ALIGN(32 / 8);
  _end = .;
  _bss_end__ = . ; __bss_end__ = . ; __end__ = . ;
  PROVIDE (end = .);
  .image	:
  {
    *(.image)
  } > gzboot
}
