/* $NetBSD: module.ldscript,v 1.2 2018/12/29 00:35:21 christos Exp $ */

/* linker script for generating RISC OS relocatable modules */
/*
 * The important thing here is that we need the text segment to be at the
 * start so that the module header ends up in the right place when we
 * flatten this into a binary file.  We also don't bother page-aligning
 * anything, because RISC OS won't.
 */

OUTPUT_ARCH(arm)

PHDRS
{
  text PT_LOAD;
  headers PT_PHDR FILEHDR PHDRS;
}

SECTIONS
{
  .text		: { *(.text) *(.gnu.warning) } :text
  _etext = .;
  PROVIDE (etext = .);
  .rodata	: { *(.rodata) }
  .data		: { *(.data) }
  _edata = .;
  PROVIDE (edata = .);
  __bss_start = .;
  .sbss		: { *(.sbss) *(.scommon) }
  .bss		: { *(.bss) *(COMMON) }
  _end = .;
  PROVIDE (end = .);
}
