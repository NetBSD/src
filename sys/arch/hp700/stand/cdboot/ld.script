/*	$NetBSD: ld.script,v 1.1.6.2 2008/06/02 13:22:08 mjf Exp $	*/

/*	$OpenBSD: ld.script,v 1.1 2003/03/28 22:42:26 mickey Exp $	*/

OUTPUT_FORMAT("elf32-hppa")
OUTPUT_ARCH(hppa)
ENTRY(begin)

SECTIONS {

	/* Read-only sections, merged into text segment: */
	. = + SIZEOF_HEADERS;
	.text : {
		*(.text)
		*(.text.*)
		*(.rodata)
		*(.rodata1)
		*($CODE$)
		etext = .;
	} = 0x08000240

	/* Read-write sections, merged into data segment: */
	.data : {
		$global$ = .;
		*(.data)
		*(.data1)
		*(.sdata)
		*(.sdata2)
		*(.dynamic)
		CONSTRUCTORS
		edata = ABSOLUTE(.);
	} = 0

	.bss : {
		__bss_start = .;
		*(.dynbss)
		*(.bss)
		*(.sbss)
		*(.scommon)
		*(COMMON)
		end = ABSOLUTE(.);
		__bss_end = .;
	}

	/* don't need an unwind in the boot, unless we'll have a boot-ddb (; */
	/DISCARD/ :
	{
		*(.PARISC.unwind)
	}
}

