/*	$NetBSD: kern.ldscript.Xen,v 1.7 2015/08/20 08:53:31 uebayasi Exp $	*/

OUTPUT_FORMAT("elf64-x86-64", "elf64-x86-64",
	      "elf64-x86-64")
OUTPUT_ARCH(i386:x86-64)
ENTRY(_start)
SECTIONS
{
  /* Read-only sections, merged into text segment: */
  .text :
  {
    *(.text)
    *(.text.*)
    *(.stub)
    *(.rodata)
  }
  _etext = . ;
  PROVIDE (etext = .) ;

  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  */
  . = ALIGN(0x100000) + (. & (0x100000 - 1));
  __data_start = . ;
  .data :
  {
    *(.data)
  }
  . = ALIGN(64);	/* COHERENCY_UNIT */
  .data.cacheline_aligned :
  {
    *(.data.cacheline_aligned)
  }
  . = ALIGN(64);	/* COHERENCY_UNIT */
  .data.read_mostly :
  {
    *(.data.read_mostly)
  }
  . = ALIGN(64);	/* COHERENCY_UNIT */
  _edata = . ;
  PROVIDE (edata = .) ;
  __bss_start = . ;
  .bss :
  {
    *(.bss)
    *(.bss.*)
    *(COMMON)
    . = ALIGN(64 / 8);
  }
  . = ALIGN(64 / 8);
  _end = . ;
  PROVIDE (end = .) ;
  .note.netbsd.ident :
  {
    KEEP(*(.note.netbsd.ident));
  }
}
SECTIONS
{
  .text :
  AT (ADDR(.text))
  {
    *(.text)
  } =0
}
