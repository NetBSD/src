/*	$NetBSD: kern.ldscript,v 1.21.4.1 2017/04/21 16:53:22 bouyer Exp $	*/

#include "assym.h"

/*
 * PAE is enabled by default on amd64, the large page size is therefore
 * 2MB (and not 4MB!).
 */

__PAGE_SIZE = 0x1000 ;
__LARGE_PAGE_SIZE = 0x200000 ;

ENTRY(_start)
SECTIONS
{
	.text :
	{
		*(.text)
		*(.text.*)
		*(.stub)
	}
	_etext = . ;
	PROVIDE (etext = .) ;

	/*
	 * Push the rodata segment up to the next large page boundary so that we
	 * can map the text segment with large pages.
	 */
	. = ALIGN(__LARGE_PAGE_SIZE);

	__rodata_start = . ;
	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
	}

	. = ALIGN(__LARGE_PAGE_SIZE);

	__data_start = . ;
	.data :
	{
		*(.data)
	}

	. = ALIGN(COHERENCY_UNIT);
	.data.cacheline_aligned :
	{
		*(.data.cacheline_aligned)
	}
	. = ALIGN(COHERENCY_UNIT);
	.data.read_mostly :
	{
		*(.data.read_mostly)
	}
	. = ALIGN(COHERENCY_UNIT);

	_edata = . ;
	PROVIDE (edata = .) ;
	__bss_start = . ;
	.bss :
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(64 / 8);
	}

	. = ALIGN(__LARGE_PAGE_SIZE);

	/* End of the kernel image */
	__kernel_end = . ;

	_end = . ;
	PROVIDE (end = .) ;
	.note.netbsd.ident :
	{
		KEEP(*(.note.netbsd.ident));
	}
}

SECTIONS
{
	.text :
	AT (ADDR(.text) & 0x0fffffff)
	{
		*(.text)
	} = 0
}
